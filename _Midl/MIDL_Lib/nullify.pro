Pro Nullify, pre, first = v_0, second = v_1, third = v_2, fourth = v_3, $
	fifth = v_4, sixth = v_5, seventh = v_6, eighth = v_7


;+
; NAME:
;		NULLIFY
; VERSION:
;		8.21
; PURPOSE:
;		Called with up to 8 keyword parameters and a code representing their
;		previous status ('defined' - 'undefined', as generated by HOW_MANY), 
;		NULLIFY returns all previously undefined parameters to undefined status.
; CATEGORY:
;		Programming.
; CALLING SEQUENCE:
;		NULLIFY, FIRST =... , PREVIOUS_STAT = PRE
; INPUTS:
;	PRE
;		Binary representation of the previous status of the input variables, in
;		same format as the WHICH_ONES output of HOW_MANY.  For example, if the
;		variables corresponding to FIRST, FIFTH and SIXTH were previously
;		defined, PRE is 2^0 + 2^4 + 2^5 = 49.
; OPTIONAL INPUT PARAMETERS:
;		None.
; KEYWORD PARAMETERS:
;	FIRST	|
;	SECOND	|
;	THIRD	|
;	FOURTH	|	Each of these keywords accept an arbitrary input,
;	FIFTH	|	including no input or undefined input.
;	SIXTH	|
;	SEVENTH	|
;	EIGHTH	|
; OUTPUTS:
;		None.  Some of the variables may be reset to !NULL though.
; OPTIONAL OUTPUT PARAMETERS:
; 		None
; COMMON BLOCKS:
;		None.
; SIDE EFFECTS:
;		None.
; RESTRICTIONS:
;		Currently NULLIFY is restricted to a maximum of 8 variables.  If
;		needed, the number can be increased.
; PROCEDURE:
;		Straightforward.  Calls HOW_MANY and ISNUM, from MIDL.
; MODIFICATION HISTORY:
;		Created 20-JUL-2013 by Mati Meron.
;-

	on_error, 1
	nmax = 8
	vnams = strcompress('v_' + sindgen(nmax),/rem)

	if not Isnum(pre,/int) then message, 'Bad or missing previous status!)
	dum = How_many(fir= v_0, sec= v_1, thi= v_2, fou= v_3, $
	fif= v_4, six= v_5, sev= v_6, eig= v_7, whi= cur)

	pow = 2l^(lindgen(nmax))
	chfl = (pre/pow and 1) ne (cur/pow and 1)
	for i = 0, nmax-1 do if chfl[i] then dum = execute(vnams[i]+'=!null')

	return
end