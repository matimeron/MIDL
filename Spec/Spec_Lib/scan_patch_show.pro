Pro Scan_patch_show, dat, qpositive = qps, _extra = _e

;+
; NAME:
;		SCAN_PATCH_SHOW
; VERSION:
;		6.4
; PURPOSE:
;		Displays the results of scan patching.
; CATEGORY:
;		SPEC data processing.
; CALLING SEQUENCE:
;		SCAN_PATCH_SHOW, DAT, [_extra = _e]
; INPUTS:
;	DAT
;		Patched data in the standard 3 column format.
; OPTIONAL INPUT PARAMETERS:
;		None.
; KEYWORD PARAMETERS:
;	/QPOSITIVE
;		Switch.  If set, only data corresponding to positive values of Q is
;		transferred to SCAN_SHOW.
;	_EXTRA
;		A formal keyword, used to transfer additional keywords to imbedded
;		SCAN_SHOW.  Not to be used directly.
;
;		Note:	All SCAN_SHOW keywords can be used.  See there.
;
;		Note:  All plotting keywords can be used.
;
;		Note:	If any of the keywords /PNG, /JPG or /BMP is invoked in call
;				to SCAN_PATCH_SHOW, whether directly or through _EXTRA, the
;				appropriate image file will be generated by WIMG_MM.
; OUTPUTS:
;		Graphics output only, a plot of the patched data.
; COMMON BLOCKS:
;		None.
; SIDE EFFECTS:
;		None.
; RESTRICTIONS:
;		None.
; PROCEDURE:
;		Checks for good and bad data (where bad is defined as smaller or equal
;		to error), and if bad data is present, it is displayed using different
;		linestyle.  Calls SCAN_SHOW.
; MODIFICATION HISTORY:
;		Created 25-OCT-2007 by Mati Meron.
;-

	on_error, 1

	if keyword_set(qps) then begin
		dum = where(dat[0,*] gt 0)
		pdat = dat[*,dum]
	endif else pdat = dat
	bad = where(pdat[1,*] le pdat[2,*], nbad, comp = good, ncomp = ngood)
	if nbad gt 0 then begin
		err = pdat[2,bad]
		pdat[2,bad] = sqrt(err^2 + (err - pdat[1,bad])^2)
		pdat[1,bad] = err
		print
		print, 'Some data is unreliable, adjusted for plotting!'
		print
	endif

	if ngood gt 0 $
	then Scan_show, pdat[*,good],pdat,lin=[0,1],ymargin=[4,4],call=4,_extra=_e $
	else Scan_show, pdat, lin = 1, ymargin = [4,4], call = 4, _extra = _e

	return
end